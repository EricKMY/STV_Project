*** Settings ***
Library    AppiumLibrary

*** Keywords ***
Create Wallet
    Wait Until Element Is Visible On Page    id=name_edit_text    timeout=3s    error=!!!!
    Input Text    id=name_edit_text    Living Expenses
    Click Element After It Is Visible    id=action_save_changes
    Wait Until Element Is Visible On Page    //*[@text="Transactions"]    timeout=3s    error=Transactions page should be visible.
    Click Element After It Is Visible    //*[@content-desc="Open"]
    ${walletName} =    Get Text    id=material_drawer_account_header_name
    Should Be Equal    Living Expenses    ${walletName}

Wait Until Element Is Visible On Page
    [Arguments]    ${locator}    ${timeout}    ${error}
    Wait Until Page Contains Element    ${locator}    timeout=${timeout}    error=${error}
    Wait Until Element Is Visible    ${locator}    timeout=${timeout}    error=${error}

Click Element After It Is Visible
    [Arguments]    ${locator}
    Wait Until Element Is Visible On Page    ${locator}    timeout=3s    error=Element should be visible.
    Click Element    ${locator}

Delete Wallet
    Click Element After It Is Visible    id=material_drawer_account_header_text_section
    Click Element After It Is Visible    //*[@text="Manage wallets"]
    Wait Until Element Is Visible On Page    //*[@text="Manage wallets"]    timeout=3s    error=Manage wallets page should be visible.
    Click Element After It Is Visible    //*[@text="Living Expenses"]
    Wait Until Element Is Visible On Page    //*[@text="Wallet"]    timeout=3s    error=Wallet page should be visible.
    Click Element After It Is Visible    id=action_delete_item
    Wait Until Element Is Visible On Page    id=md_title    timeout=3s    error=Dialog should be visible.
    ${dialogTitle} =    Get Text    id=md_title
    Should Be Equal    Warning    ${dialogTitle}
    ${dialogContent} =    Get Text    id=md_content
    Should Be Equal    Are you sure you want to delete this wallet? All the related data will be removed.    ${dialogContent}
    Click Element After It Is Visible    id=md_buttonDefaultPositive
    Page Should Not Contain Element    //*[@text="Living Expenses"]

Open App
    Open Application    http://localhost:4723/wd/hub    platformName=Android    platformVersion=7.1.1    deviceName=Pixel 2 API 25    app=${EXECDIR}/../moneywallet/app/proprietaryOsm/release/app-proprietary-osm-release.apk    appPackage=com.oriondev.moneywallet
    Click Element After It Is Visible    id=first_start_button
    Click Element After It Is Visible    id=skip
    Click Element After It Is Visible    id=done
    Wait Until Element Is Visible On Page    //*[@text="New wallet"]    timeout=3s    error=Wallet page should be visible.

Go Back To List
    Click Element After It Is Visible    //*[contains(@resource-id, "secondary_toolbar")]/*[@class="android.widget.ImageButton"]